# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libvdpau
pkgver=1.5
pkgrel=3
pkgdesc='Nvidia VDPAU library'
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
license=(MIT)
depends=(
  glibc
  libx11
  libxext
)
makedepends=(
  git
  meson
  xorgproto
)
optdepends=(
  'libvdpau-va-gl: driver using VAAPI'
  'mesa-vdpau: driver for Mesa'
  'nvidia-utils: driver for NVIDIA'
)
provides=(libvdpau.so)
source=("git+https://gitlab.freedesktop.org/vdpau/libvdpau.git#tag=$pkgver")
b2sums=(d8f0e7d6290d51fd764f2c29443fef52aea8a6ae5c3c3a91ca2a9ed788cd74972189c20095c239b0aab3dcece54acc320be95dfe5ec65902381b8f3bfe80ac3a)

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 libvdpau/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set sw=2 sts=-1 et:
